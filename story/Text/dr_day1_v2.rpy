label dr_day1_intro:
    $ persistent.sprite_time = "day"
    $ day_time()
    $ sch_newday(sch_dayNo=1)
    $ name_sch('Я')
    scene anim prolog_2
    show yellowish:
        alpha 0.25
    with fade
    "Я чувствовал себя утопленником."
    "Тьма. Отсутствие гравитации, непонятная тяжесть по всему телу и отсутствие воздуха."
    "Здесь отказывали все органы восприятия человека, и только какое-то чутьё намекало на возможность выхода."
    play ambience ambience_ext_road_day fadein 10
    "Пустота и невесомость сменилась ощущением нахождения на жестком обитом стуле со спинкой, а в глаза начал бить яркий желтоватый свет."
    show blink
    pause(1.0)
    scene expression Sepia('bg int_bus') behind unblink
    show unblink
    pause(1.5)
    scene bg int_bus 
    show white2:
        alpha 0.2
    with flash
    "И глаза я открыл уже в другом месте - меня не окружал мой город или перрон безымянного города, окружённый холодными оттенками розового заката."
    "Человек - вообще ещё та скотина, быстро приспосабливающаяся к любым ситуациям. Потому, завидев салон Икаруса вместо знакомого Рено, я пожал плечами и принялся рассматривать интерьер." 
    "Сиденья цвета кофе с молоком, жестяной рифлёный пол, красные вельветовые шторы - потолок казался неоправданно высоким для автобуса и не совпадал по интерьеру с тем, в который я сел, однако создавал уютную атмосферу в тёплых тонах."
    "Я сразу же отметил довольно жесткие сидения и стерильную пустоту в салоне, и только множество значков с разными городами и неизвестными мне фирмами (и иконами, куда же без них) указывали на то, что автобус всё же не первый день \"на работе\"."
    th "Хоть в этот раз что-то интересное приснилось."
    play sound sneeze
    pause(0.7)
    hide white2
    with vpunch
    "От пробивающихся солнечных лучей я чихнул и таким образом немного освежил свою сонную голову."
    th "Ну и пыльно тут. Так, ну всё, пора дышать свежим воздухом."
    "С этой мыслью я направился прочь из автобуса навстречу яркому солнцу рассматривать место прибытия."
    scene bg int_bus:
        pause 0.2
        xalign 0.5 yalign 0.5
        ease 0.5 zoom 1.5
    "По пути к единственной двери из автобуса я мельком взглянул на приборную панель и остановился."
    "На ней стояли песочные часы. Из верхнего сосуда сыпалась небольшая струйка белого сверкающего песка."
    th "Кто-то отсчитывал по ним время? До отъезда, может быть?"
    "Я не особо интересовался по этому счёту, скорее просто любопытствовал у себя в голове, не ожидая никакого ответа."
    "Я выпрыгнул из салона ещё раз удивился безизъянности своего сна."
    scene bg ext_bus:
        xalign 0.5 yalign 0.5
        ease 1.5 zoom 2.0 xalign 0.15
        ease 7.0 xalign 0.85
    "Казалось, что Икарус только что совершил одну из своих первых поездок - автобус казался абсолютно новым, и единственное, что показывало небольшой стаж машины - дорожная пыль на дверях и нижней части корпуса."
    "На решётке радиатора красовался номер модели, а чуть повыше название автобуса."
    th "Да, если уж и видеть сны, то и идеализировать всё вокруг, чтобы не хотелось его покидать."
    scene bg ext_bus:
        xalign 0.5 yalign 0.85
        ease 1.0 xalign 0.5 yalign 0.5
    th "Ладно. Где он хоть остановился?"
    scene bg ext_camp_entrance_day with dissolve
    th "Ну, приехали."
    "Я поёжился своему несмешному каламбуру, однако лучшей оценки моей фантазии придумать не мог."
    "Мой сон привёл меня к лагерю. Пионерскому."
    "Сначала я даже подивился несвойственной для снов детализации, но, присмотревшись, убедился в своих догадках."
    scene bg ext_camp_entrance_day:
        xalign 0.5 yalign 0.5
        ease 1.5 zoom 2.0 xalign 0.3 yalign 0.95
        ease 5.0 xalign 0.7
    "Нет, я не ошибся. На больших стальных воротах, немного отворённых и достаточно старых, чтобы с них начала слезать краска, красовалась надпись русскими буквами: \"пионерлагерь\"."
    "Вход, как горгульи, окружали два гипсовых пионера на небольших аскетичнх постаментах."
    "Сам асфльт был потрескан и где-то среди его кусков ближе к бордюру вылезала трава."
    th "Да, жаль, что мне приснился не Артек."
    "А через несколько секунд, когда мне наскучило смотреть на жестяные ворота, я повернул к дороге, по которой приехал этот автобус."
    scene bg ext_road_day with dissolve 
    "Передо мной предстал невиданный, даже сказочный пейзаж, никак не от мира сего." 
    "Мне сначала показалось, что то, что я вижу - это гипертрофированный сон с выкрученной в лайтруме яркостью, настолько пейзаж, представленный мне, казался нереалистичным." 
    "Каждая травинка пестрила лучами солнечного света, пробивающимися через полупрозрачные волокна, и продолжала их." 
    "Мне хотелось прыгнуть в это зелёное одеяло с головой и лежать там, выглядывая облака и их фигуры до самой ночи, чтобы потом узнавать созвездия в безоблачном небе." 
    stop music fadeout 2 
    th "А такое мой мозг уже не мог сгенерировать."
    "Будучи шокированным и ведомым желанием узнать, сон это или нет, я ущипнул себя, думая, что сейчас же проснусь."
    with flash_red
    play music premonition fadein 10
    "Было больно."
    th "Больно?! Я не сплю?!"
    play sound sfx_slap
    with flash_red
    "Пощёчина была ещё больнее, однако не только не вывела меня из состояния сна, но и саднила ещё несколько секунд после удара."
    ivan "Не сплю?! Я не могу не спать!"
    "Наслаждение иллюзией переросло в панику, и мой мозг был парализован одной только мыслью о реальности происходящего."
    play sound sfx_inhale
    "Я выдохнул, попытаясь успокоиться."
    ivan "Но тогда... где я?"
    scene bg ext_camp_entrance_day with dissolve
    th "И лагерь... Не выдумка?"
    "Страшные, воистине сказочные фантазии начали бегать по моей черепной коробке и соскрёбывать костную ткань, перехватывая сигнал здравого смысла."
    "Я приказал себе не паниковать, а думать рационально. Вышло плохо. Сознание выплюнуло что-то вроде \"Да какая к чёрту рациональность?\" и замолкло."
    "И, тем не менее, современный человек знает способы сказать \"привет\" цивилизации из самых удалённых уголков."
    scene bg ext_bus with dissolve
    "Я подошёл к двери автобуса и присел на ступеньку в надежде спрятаться от слепящего света в темноватом салоне."
    "Линзы не ловили ни сеть, ни GPS. Что уж говорить о таких ситуационных технологиях, как Wi-Fi и Bluetooth."
    show blink
    pause(2.0)
    window hide
    hide blink
    scene bg ext_bus
    show unblink
    hide blink
    window show
    "Перезагрузка не спасла, и все надежды на сбоивший модуль связи обрушились крестиком над импровизированным значком антенны."
    ivan "И что это значит?"
    "А значит это только одно. {w}Я в самом центре нихрена, и дай бог здесь работают ЛЭП и есть электричество."
    th "Видимо, кто-то очень хотел меня отрезать от мира. Придётся играть по правилам."
    with fade
    "Прошло некоторое время. Песочные часы на приборной панели даже не покрыли дно нижнего сосуда, и стук отдельных песчинок о стекло меня раздражал."
    th "Как же мне это всё надоело."
    if persistent.iv_true_sch:
        menu:
            "Опрокинуть солнечные часы.":
                call dr_sandclock_break
            "Оставить часы в покое.":
                $ sch_true_flag = 1
                "Чтобы не сорваться, я решил успокоиться и сконцентрироваться на чём-то одном."
                "Например, на виновнике моего состояния - на часах."
                "Каждый раз, когда я на них смотрел, мне казалось, что за ними стоит нечто большее, чем отсчёт до конца перекрура или до чего там засекал время неизвестный водитель советского автобуса."
                "Будто пока они идут, я не могу их тронуть, иначе прерву что-то очень важное."
                "На этом моменте стекло начало переливаться небольшой радугой и песок вернулся наверх и стал течь снова."
                "Я удивился, однако не придал этому никакого хоть какого-то значения, приняв такое облегчение как должное."
                # и тут что-то потом будет происходить другое, но я не хочу об этом задумываться сейчас
    else:
        call dr_sandclock_break
    stop music fadeout 5
    return

label dr_sandclock_break:
    th "К чёрту их!"
    play sound glass_break
    with vpunch
    "Я поднялся и махом руки со всей злости разбил так бесящие для меня часы."
    "Мне казалось, будто они что-то отсчитывали от меня, отщипывали по кусочку, и единственным способом побороть это странное и гнетущее ощущение было разбивание этого корпуса."
    "Песок рассыпался по ступеням и прибрной панели, выделяясь небольшими блёстками на поверхности бело-жёлтых гранул."
    "Давящие ощущения прекратились и испарились из меня вместе с вопросами о причине появления в этом месте."
    "Я покинул автобус и чётко, на уровне интуиции для себя понял - уехав на нём, меня не будет ждать мой мир."
    return


label dr_day1_meeting:
    play ambience ambience_ext_road_day fadein 1
    scene bg ext_camp_entrance_day with dissolve
    "Двери начали тихо отворяться и из них вышла девушка."
    play music music_lis["a_promise_from_distant_days_v2"] fadein 5 # скорее всего расово неверная
    show un serious pioneer far with dissolve
    "Я машинально отметил её красоту и необычное одеяние."
    "Одета она была в пионерскую форму - самую, что ни на есть настоящую. Белая рубашка с нашивкой на рукаве, красный значок с дедушкой Лениным, синяя юбка и, естественно, самый главный отличительный элемент - кумачовый галстук."
    th "Встречать пришла?"
    "К такой мысли я пришёл не сразу, ибо она была явно чем-то недовольна и находилась в себе, упор не замечая меня."
    th "Я решил проявить немного доброты, состарадания и избавиться от неловкости, завязав разговор."
    ivan "Кхм."
    th "Ноль внимания."
    "Я прочистил горло ещё раз, но уже погромче."
    ivan "Кхем-кхем."
    show un surprise pioneer far with dspr
    "Девочка тихо ойкнула и наконец обратила внимание на автобус и меня."
    ivan "Привет."
    un "Т-ты откуда?"
    th "Хороший вопрос. Простой и ни разу не очевидный."
    ivan "На автобусе приехал. Не видно?"
    "Тёмноволосая удивилась ещё сильнее."
    un "К-каком?"
    "Я развернулся, чтобы постучать по кузову, однако костяшки моих пальцев разве что постучали по воздуху."
    show bg ext_no_bus with dissolve
    ivan "На таком..."
    "Автобуса не было."
    th "Что? Как этот Икарус успел превратиться в бесшумный пепеллац и уехать без водителя?"
    th "Очередная магия сна?"
    ivan "Да уехал уже, как видишь."
    "Опозориться при первой встрече надо ещё уметь, однако и это у меня получилось."
    un "П-понятно."
    ivan "А как тебя зовут?"
    "Правильно. Что ещё спросить у незнакомки?"
    return
