label sch_day2_start:

    $ persistent.sprite_time = "day"
    $ day_time()

    "Жарко, душно, голова болит неистово..."

    ivan "Где я?"

    iv "Кажется, я начинаю вспоминать..."
    "Я открыл глаза."

    ivan "А, точно."
    ivan "Прыгнул в окно с непойми какого этажа и разбился насмерть."

    iv "Очнулся - глюки. Бэдтрип."
    "Повторил я про себя на всякий случай."

    ""
    "Непривычно-неудобная кровать своим скрипом вернула меня в суровую реальноссть."
    "Я ещё раз осмотрелся."

    ivan "Так-так, и что тут у нас?"

    if sch_clock:
        "Часы на столе с приятной неоновой подсветкой указывали на то, что сейчас около семи часов утра."
        "А вожатой всё еще нет."

    else:
        "Вожатой... как там её? Ольги Николаевны? Нет, Ольги Дмитриевны на кровати не было."
        "Зато на спинке кровати висел неприлично большой предмет нижнего белья. Кстати, довольно милый. Розовый, с кружевными цветочками. А под ней лежал огромный молот. У меня бы его назвали банхаммером."
        iv "Видимо, не всё выветрилось из моего извращённого интернетом мозга."
        iv "Я проморгался в надежде, что данное орудие будет лишь моей галлюцинацией, но нет - и на второй раз ононе изменило себе и лежало под слоем пыли."

    iv "Либо она не человек, либо летучки проходят настолько рано."
    ivan "Anyway..."
    "Он говорит на расово и национально неверном языке! Захватить - и к стенке!"
    iv "Мой мозг по утрам - самый мощный генератор пошлых мыслей и тупых идей."
    iv "А тем временем лучше бы он включил в моих глазах автофокус и нашёл новую форму."
    "Через десять секунд отыскалась и моя форма, лежащая в груде других интересных вещей."
    "Что у нас будет?"

    if 'el' or 'sh' in list_sch_ch_known:
        "Судя по одежде пионеров, меня ждёт очередное и самое привычное унижение."
        "И тугой пионерский галстук, затянутый над моей шеей мертвым и цепким узлом."

    else:
        "Пожалуйста, пусть это не будет походить на летний вариант школьной формы!"
        "..."
        iv "Кажется, я умею предсказывать будущее."
        iv "Или я, балда, не догадывался, что всё так и будет."

    "С другой стороны, что ещё можно было ожидать?"
    if pt_sl >=4:
        "Типичные шорты и рубашка, идеально подобранные по моему размеру, аккуратно висели на спинке стула, стоящего возле моей кровати."
        "Почему-то мне не представляется возможным, что Ольга Дмитриевна за 3 минуты знакомства со мной смогла так идеально определить мой размер одежды."
    else:
        "Типичные шорты и рубашка, идеально подобранные по размеру зорким глазом, видимо, вожатой."
    iv "\"Воротничок настолько белый и хрустящий, что даже нацисты сомневаются в чистоте своей расы.\""
    "А ещё он сшит из чистого хлопка и скрипит не меньше, чем кровать, на которой я провел самую странную ночь в моей жизни."
    "В карманах шорт я обнаружил довольно большой, но аккуратно сложенный платок."
    "Завязывать галстук по-пионерски меня ещё учила моя бабушка, хоть СССР уже давным-давно распался."
    ivan "Никогда бы не подумал, что мне пригодится навык завязывать пионерский галстук."

    "Моя куртка (странно, что не спросили, откуда я приехал!) висела на деревянной вешалке, идеально выглаженная и чистая, без пятен талого снега, а содержимое моих карманов находилось тут же, на тумбочке рядом с кроватью."

    if sch_keys:
        "Славины ключи, к счастью для самой Слави, у меня не обнаружили, а потому они спокойно лежали в углу ящика."
        "И всё-таки я чувствую, что им комфортнее в соседнем кармане с моим телефоном."
        "Как камера или фонарик он ещё может пригодиться. А ключи - тем более!"

    else:
        "Вооружившись телефоном, который мог послужить лишь камерой или фонариком, и пакетиком с явным запахом мыла и прочих очистительных принадлежностей, я направился к умывальникам."

    # фон
    "Лагерь спал. Не то чтобы там было несколько сонных пионеров, нет - лагерь казался пустым и сопел из каждого домика и затягивал в такое же настроение меня."
    "Но режим сна не пропьёшь."
    "Сейчас в лучшем случае пол седьмого, до пробуждения масс ещё полтора часа."
    ivan "Свобода, тишина, свежий воздух."
    "Старому мне она могла и приесться, но тот Ванька, в чём теле я нахожусь, явно получал тонну удовольствия от вдыхания почти идеального сочетания кислорода и азота, в котором токсичным примесям места просто не было."
    "Огранизм всё ещё не мог привыкнуть к этим условиям, поэтому голова немного кружилась."
    iv "Как сказал бы Олег Степаныч: \"тебе нужно с собой выхлопную трубу брать\"."
    "И после этого залился бы громким неостановимым смехом. Олежка, двацать девять лет."
    "Человек, который подтолкнул меня к нынешнему состоянию. Может быть, без него я бы сейчас и не попал в это райское место."


    # умывальники

    "В раздумьях я добрался до умывальников."

    ivan "Ставлю тысячу рублей на то, что вода тут будет ледяная."
    sl "Вот это ставки!"

    show sl smile2 sport at left with dissolve

    "Славя появилась из ниоткуда, то ли подкравшись со спины, то ли следуя за мной из интереса. Как кошка."
    "Я чуть ли не рефлекторно отскочил и оберунлся в её сторону."
    "Блондинка была одета в спортивную форму, похожую на фирменный Adidas - чёрно-белые ультра короткие шорты и футболка с двумя полосками по бокам."
    "Врать не буду - в пионерской форме она выглядит отлично, а в этих шортиках ещё лучше!"

    ivan "Мне кажется, или такую брендовую одежду в СССР не продают?"
    sl "Мне Мику подарила в этом году, она второй раз приехала и знала, что мне было бы неплохо сменить спортивную форму."
    ivan "Сама щедрость. А ты который раз тут?"
    sl "Где-то четвертый."
    iv "И как ей не надоело?"
    ivan "Ты же вроде образцовая пионерка, помощница вожатой, разве тебя не должны были отправить в лагерь поприличнее?"

    show sl normal sport at left with dspr

    sl "Мне предлагали."
    ivan "И ты?"

    show sl sport laugh at left with dspr

    sl "И я взяла и уехала в Артек! Видишь, меня нет!"
    ivan "Издеваешься над невыспавшемся человеком."
    "Пробурчал я в ответ."

    show sl normal sport at left with dspr

    sl "Кстати, с чего это ты так рано умываться идёшь? Спать не давали?"
    ivan "К тебе тот же вопрос."

    show blink

    "Она в чём-то была права - спать я себе не давал - слишком много думал{w} - и о моей смерти, и об этом лагере..."

    show unblink
    hide blink

    sl "Я привыкла просыпаться и совершать пробежку, пока все спят и Ольга Дмитриевна ещё не начала новый день в лагере и не заняла меня."
    ivan "Это не кажется тебе эксплуатацией?"
    sl "Ты что, она минуты с вами отдохнуть не может!"
    iv "Что-то мне не особо верится."
    "К тому же вставать в семь, чтобы потом работать до двенадцати? Увольте меня от такой работы."
    sl "А ты?"
    ivan "Что \"я?\""
    sl "Почему так рано проснулся?"
    ivan "Режим сна, который у меня установлен, я даже снотворным не нарушу."
    sl "Ранняя пташка?"
    ivan "Можешь и так назвать."
    "Разговор заходил в тупик, а Славя так и не давала мне пройти к умывальникам."
    ivan "Может, всё же пропустишь? Я десять минут уже жду, когда одна золотоволосая особа перестанет блокировать проход к живительной водичке."
    show sl tender sport with dspr:
        xanchor 0.5
        pause 0.5
        easein 0.5 xanchor 0.0
    sl "Прости."
    hide sl with dissolve
    sl "Только аккуратнее - она очень холодная!"
    "Послышался голос сзади. Не отдаляющийся."
    "Однако Славя скрылась за моим полем зрения."
    ivan "Раз, два, три!"
    "Б-р-р-р!"
    "Холодная, чёрт возьми! Славя не врала про слово \"очень!\""
    "Я, наверное, за весь свой опыт геологом такую холодную воду не встречал, тем более летом."
    ivan "One, two, three!"
    "По сути я просто подбрасывал воду к лицу, так как руки просто замерзали, и пальцы больше походили на неподвижные крючки."
    ivan "Ein, zwai... Славя!"
    "Я резко развернулся, услышав как тихие прыски в кулак превратились в смех."
    show sl smile2 sport with dissolve
    sl "Всё, ухожу, ухожу!"
    show sl tender sport with dspr:
        xanchor 0.5
        pause 0.5
        easein 0.5 xanchor 1.0
    "В этот раз я проследил, чтобы Славя ушла."
    hide sl with dissolve
    "Хоть я и не уверен, что она всё ещё следит за мной."
    iv "Никакой личной жизни."
    "Чуть не сказал я вслух, пока не вспомнил, что не сижу в своём кабинете."
    "Я достал из пакетика зубной порошок. {nw} А потом посмотрел на раковину с жидким льдом."
    iv "Если бы мои зубы имели разум, они бы резко начали дергать нервную систему, чтобы я потерял сознание от шока и не обливал их криожидкостью."

    with fade

    $ renpy.pause(3, hard=True)
    # звук умывальника

    "На самом деле, всё было не так уж и плохо."
    "По крайней мере, могло быть и хуже."
    iv "Хорошо, план-минимум выполнен."
    "Может, Славя была права и мне стоит пойти покемарить ещё часик, пока могу?"

    menu:
        "Да, определённо спать":
            # фон
            "С этой мыслью я двинул обратно в сторону домика."
            "Некоторые жильцы уже просыпались и не давали спать другим."
            #внешка домика
            "Летняя сирень, красота."
            "Я осторожно открыл дверь, и..."
            # цг ОД переодевается
            iv "Вот это вид!"
            iv "Четвертак бросаешь в приёмник, и дверная скважина открывается - это мог быть такой хороший заработок!"
            "Да за такое я бы целый доллар отдал! Посмотреть на то, как вожатая с {i}такими{/i} формами переодевается..."
            "Но появилась одна не очень приятная новость."
            "Ни организм, ни я не хотели закрыть дверь."
            iv "Нет-нет-нет и ещё раз нет. Нельзя."
            iv "Моя рука сейчас закрывает дверь."
            iv "Я сказал, что моя рука берёт ручку и закрывает дверь..."

            if pt_wi >= 20:
                if pt_ka >= 20:
                    $ sch_day2_od_photo = True
                    "Возможно, что мы ещё аккуратно достаём второй рукой телефон и делаем быстрый снимок..."

                    with flash

                else:
                    "Возможно, я мог попытаться сфотографировать, но..."

                scene bg ext_house_of_mt_day with dissolve
                iv "Теперь точно закрыли дверь!"
                "Ни единого скрипа петель или щелчка замка."
                ivan "Фух..."
                "Я приземлился на шезлонг так же бесшумно."
                iv "Так делать нельзя - определённо. Сделал бы я в свои 17 так?{w} Определённо нет."
                iv "Экспериментально проверили, что гормоны контролировать я не могу."
                iv "Или экстремально. Или..."

                show black with fade

                "Я успел только заметить, мир начал темнеть."

                $ renpy.pause(1, hard=True)

                "Меня кто-то толкнул за плечо."
                hide black
                show unblink
                "Пришлось просыпаться."
                # фон и вожатая
                mt "Ваня, вставай, проснись и пой! Сегодня для тебя начинается первый полноценный день в нашем лагере!"
                ivan "На самом деле, не очень хорошая мотивация."
                mt "Не проснулся? Ничего. Умываться ходил уже?"
                ivan "Да, мам."
                # недобрая ОД
                mt "Не фамильярничай со своей вожатой."
                ivan "Извините, исправлюсь и докажу."
                "Вставать с шезлонга не хотелось совершенно. Да и отвечать на фразы Ольги после такого тоже."
                mt "Вставай давай. Через пятнцадцать минут линейка. У тебя 15 минут на всё."
                iv "Цигель-цигель!"

            else:
                #цг вожатая обернулась
                "Ольга Дмитриевна оберуналсь.."
                mt "Что? {w} Иван!"
                "Я хлопнул дверью настолько быстро, насколько смог и побежал в сторону умывальников под крики вожатой."
                # Фон под фильтром серо-розовым
                "Чем холоднее, тем лучше!"
                iv "А-а-а-а!"
                # убрать фильтр

                "Я умылся пару раз, чтобы остудиться и оценить ситуацию."
                iv "При встрече меня ждёт а-та-та, но только если я окажусь с ней один на один. Либо же она предпочтёт не разглашать то, о чём могут подслушать различные разведчики."
                iv "В любом случае, лучше перестраховаться."
                "Подумал я, а после решил -"
                iv "К чёрту это всё! Если вожатая и ищет меня, то к домику не вернётся. К тому же у неё летучка и ей нужно будить пионеров."
                scene bg ext_house_of_mt_day with dissolve
                iv "А ещё лучше, если я просто подожду на площади."
                # площадь утро with dissolve
                "Я вышел из улочек и сел на скамейку прямо под тенью Генды."
                # блинк
                $ sch_day2_od_failed = True
                return
            return


        "Прогулка придаст бодрости!":
            $ pt_wi +=20
            $ pt_pi -=10
            $ pt_un +=2
            $ list_sch_noir_flag = 'forest_un'
            "И всё же нет. Нельзя спать после сна - утром будет только хуже."
            "Потому я отправился..."
            # фон леса
            "В лес!"
            "К тому же, согласно карте, выход к нему идёт как раз через умывальники."
            # фон тропа в лесу
            "Тропинка стала ощутимо меньше и начала идти вниз, однако не прервалась, а наоборот, даже казалась более исхоженной, чем большая."
            iv "Что, собственно, очевидно - площадь ведь меньше."
            "Спросонья мне приходилось объяснять себе даже такие простые вещи, как истоптанность тропинки, а какой там был лес я даже и не задумывался."
            "Просто гулял, дыша свежим воздухом и пиная редкие камушки."
            "Настолько неторопливо, что не заметил, как потратил уже пол часа на хождения и вернулся обратно в лагерь."
            "Однако в этот раз меня встретили."
            # лена норм
            if 'un' in list_sch_ch_known:
                # лена смущение
                "Лена."
                if 'un' in list_sch_day2_walk:
                    # мимолётная улыбка с возвращением в смущение
                    "Вчера ночью на площади она согласилась прогуляться со мной по лагерю, и уже меньше, чем через 8 часов, я вижу её снова."
                else:
                    "Вчера я видел её, но так толком и не поговорил."
                    iv "Какая-то она замкнутая."
                    iv "Или же я ничего не понимаю."
                ivan "Привет, Лен."
                # лена смущение
                un "Привет."
                ivan "Как дела? Тоже не спится?"
                "А о чём ещё спрашивать?"
                un "Да."
                ivan "Может, тогда прогуляемся по лесу вместе, раз встретились?"
                if 'un' in list_sch_day2_walk:
                    ivan "К тому же, ты мне обещала прогулку по лагерю вчера."
                    # фон лена норм
                    un "Раз обещала, тогда пойдём."
                    iv "Девочка отреагировала крайне спокойно - ни смущения, ни презрения в её глазах я не видел."
                    iv "Да что смущения? Я смирения не видел!"
                    iv "Как будто она хотела со мной погулять."
                    iv "В таком случае кто я такой, чтобы отказать?"
                    if pt_un >=7:
                        ivan "Позволите ли дать свою руку кавалеру?"
                        "Лена вложила свою руку в мою и мы вместе пошли по лесу."
                        un "Хорошо."
                        return
                    else:
                        "Я неловко развернулся в сторону леса и встал на одном уровне с ней."
                        ivan "Пойдём?"
                        un "Да."
            else:
                "Та фиолетововолосая девочка, которую я встретил вчера случайно по пути."
                "Должно быть, ей тоже не спится."
                ivan "Привет."
                # мимолётное смущение обратно в норм
                "Она смутилась, увидев, как я остановился и что-то сказал в её адрес."
                un "П-привет."
                "Девочка не выглядела сонной, скорее наоборот - она шла в лес с какой-то целью."
                ivan "Тоже не спится?"
                un "..."
                ivan "Извини за глупый вопрос и мои манеры. Я Иван."
                "Ваш Рыцарь в ржавых доспехах."
                un "Лена."
                ivan "Очень красивое имя."
                un "Так не я выбирала же."
                ivan "Тогда у твоих родителей хороший вкус."
                # мимолётное смущение обратно в норм
                un "С-спасибо."
                "Девочка опять выдавила из себя слова. Похоже, она ни с кем не здоровается и никого не благодарит."
                iv "Самостоятельная одиночка. Почти как я. Может, я смогу найти с ней общий язык?"
                ivan "Как насчёт совместной прогулки по лесу?"
                un "Куда мне деваться."
            "Сказала она это с какой-то своей обречённостью."
            "Я так делал, когда что-то очень хотелось, то требовалось делать вид, что соглашаешься с отвращением."
            "Однако я вышел на один уровень с ней."
            if 'un' in list_sch_day2_walk:
                "Мы шли, как маленькие дети, держась за руки и молчали каждый о своём."
                "Причём не то я, не то Лена заметно нервничала, отчего в нашей связке я чувствовал некую тряску пальцев."
            else:
                "Мы шли в одну ногу друг с другом и молчали о чём-то своём."
            iv "Не могу найти тему. Неужели я такой скучный человек?"
            ivan "Кстати, как тебе лагерь?"
            un "Н-неплох."
            "Видимо, я её напугал своей внезаной речью."
            ivan "Вижу, не в первый раз?"
            un "Да. В прошлом году тоже здесь была."
            ivan "А подробнее расскажешь? Может, в каких кружках учавствуешь или чем-то необычным занимаешься?"
            un "Ну... Рисую."
            with flash_red
            iv "Да ну. Слишком мне везёт на художниц."
            "В голову бросились воспоминания, связанные с Аминой."
            iv "И ведь всё так хорошо начиналось."
            if not (sch_bound or sch_true):
                iv "Хоть я и не ищу здесь отношений (не ищу ведь, да?), но таких опасных контактов мне лучше избегать любой ценой."
            un "Иван..."
            "Похоже, я задумался и заставил девочку скучать."
            ivan "Извини, просто подумал, насколько хорошо быть художником - можно изобразить что угодно, и фотоплёнка тебе не нужна!"
            ivan "И называй Ваней, мне не двадцать пять лет."
            iv "Да. Тут не двадцать пять."
            iv "И пора бы не выпадать, блин, из мира!"
            un "Хорошо, Ива...{w} Ваня."
            ivan "Так намного лучше. Спасибо."
            ivan "Ты что-то говорила про рисование. Самоучка или куда-то ходишь?"
            un "Нет, сама учусь."
            ivan "И сколько лет уже?"
            un "Шесть где-то."
            iv "Вот это опыт! За шесть лет только бездарность не научится рисовать, а, зная Лену настолько, насколько я могу знать её сейчас, был уверен, что она бы не стала говорить про себя то, в чём она не уверена."
            iv "Но неуверенность в себе ей всё в тапки гадит."
            ivan "А покажешь что-нибудь? Или личное? Пойму, если откажешь. У меня была девушка, тоже художницей..."
            iv "Ай-ай-ай!"
            "Теперь по голове хотелось дать самому себе."
            un "Девушка..."
            "Слово Лены повисло в воздухе."
            ivan "Извини, не это имел в виду. Она мне помогала с описанием географии мест."
            "Не похоже, что это её утешило."
            iv "Вот я молодец, в такой идеальной ситуации всё разрушить!"
            un "Покажу. Но у меня одни наброски..."
            ivan "А по-моему, в набросках больше души, чем в готовых рисунках! Там и неопределенность линий, и пространство для того, чтобы мозг сам выбрал идеальную линию и сочетание цветов."
            un "Согласна."
            if sch_clock:
                "Я посмотрел на время."
            else:
                ivan "Лена, извини, который час?"
                "Лена посмотрела на свои небольшие часики - с таким же фиолетовым, как и её волосы, ремешком."
                un "Пол девятого. {w} Ой..."
            "Разговор и прогулка стоили нам почти двух часов, и поэтому линейку мы давно прогуляли, а завтрак настанет через пол часа."
            iv "Ага. Внезапно пионер и пионерка не пошли на линейку, и в домиках их никто найти не может."
            iv "Наверное, в кружках решили позаниматься. С утра пораньше."
            iv "Brace yourself."
            ivan "Похоже, что пора возвращаться. Помнишь обратную дорогу?"
            un "А зачем?"
            iv "То есть зачем?!"
            iv "То есть мы сейчас просто шли по лесу в никуда, чтобы потеряться?"
            un "Лес нас приведёт обратно."
            "Сказала она с интонацией на слове \"лес\", будто это чьё-то имя."
            ivan "Дорожка идёт по кругу? Я не видел развилки."
            un "..."
            "Через пять минут вы и вправду вернулись обратно к умывальникам."
            iv "Да это же чёрная магия!"
            # лена лыбы давит
            un "Ой и влетит нам от вожатой."
            ivan "И что?"
            un "Да ничего."
            "Она лукаво улыбнулась - мол, - \"Да, ничего не будет\"."
    return



label sch_day2_breakfast:
    "Наконец-то, меня допустили до самого свтяого места имени предания чревоугодию."
    "Я чувствую запах еды среднего качества за версту. М-м-м, макароны! И какао!"
    "Во рту не было маковой росинки, потому я закрыл рот, чтобы не пустить слюну."
    "В числе прибывших на место я был не самый первый - два пионера возраста среднестатистической Ульянки, как мотыльки, тянущиеся к свету, упёрлись в закрытые на ключ двери столовой, через окна которой можно было увидеть, как работники лишний раз протирают столы и переставляют стулья на свои места."
    "Я же, как сторонник мирного решения конфликтов, сел на ближайшую скамейку и начал смотреть в потолок."
    "Занятие, безусловно, интересное, но мне помогает отвлечься от мыслей, будто я их выплескиваю из черепной коробки с горлышком в районе затылка."


    scene bg int_dining_hall_people_day with dspr
    show dv shocked pioneer2 with dissolve
    "Я взял поднос и собирался сесть куда-нибудь за ближний к Ульяне столик, хотя бы ради мести, но в итоге пришлось сыграть в мастерский уворот от Двачевской, создав иллюзию отсутвия зрачков в линзах, от чего Алиса испытала невиданное раньше чуватво страха, простояв в ступоре ещё несколько секунд."
    hide dv with dspr
    "К сожалению, вск свободные места заняли как только я на них посмотрел, а попытавшись нагреть карму на алгоритмы и зажмурившись, остался стоять в столовой с двумя свободными местами - с Леной и Алисой."
    "Ещё был вариант сесть рядом со стенкой, но с младшими пионерами кушать было не особо приятно."
    if sch_day2_sl_keys_given:
        "Тут же Славя, заметив меня, тактически убрала стул под стол так, будто мест не было, и помахала в мою сторону рукой."
    menu:
        "Сесть по Славей" if sch_day2_sl_keys_given:
            $ pt_sl +=1
            show sl normal pioneer close with dissolve
            "Я кивнул в её сторону и уселся рядом."
            ivan "Спасибо, что заняла. Не особо хотелось ждать и смотреть, как еда стынет."
            sl "Не за что. Приятного аппетита."
            ivan "Взаимно."
            "Мы почти синхронно опустили голову в сторону еды."
            iv "Что у нас? Макароны, две сосиски с характерным запахом столовой еды и тёплый компот из клюквы и яблок."
            "Моё чувство мне всё же не изменило."
            "Я принялся было сразу глотнуть освежающего напитка, как только начал плеваться."
            "Сахара в этой алой жиже не было совсем, зато кислоты - на тройную дозу!"
            "Зубы и язык резко заныли, а я начал издавать приглушённый гневный стон."
            show sl smile2 pioneer close with dissolve
            "Славя смотрела на это с любопытством и на мой вопрошающий взгляд решила всё же объяснить, что за пойло тут подают."
            sl "Тут в лагере в компот не добавляют сахар - некоторые ребята чувствовали себя плохо из-за того, что съели слишком много сахара, и администрация решила уменьшить его количество."
            "А компот-то тут при чём?"
            ivan "А они не могли подумать, что в этом виноват отнюдь не компот?"
            "Некоторые поставки какосодержащих продуктов, например."
            sl "Ну, старый компот и вправду был довольно сладкий. Я больше люблю, когда он кислее."
            ivan "Но не настолько, чтобы с серной кислотой конкурировать!"
            "Славя лишь рассмеялась и предложила заняться горячей пищей."
            hide sl with dspr
            "Макароны были привычной консистенции плохо схватывающегося клея - когда я отправил первую вилку в рот, всё прилипло к зубам и совершенно не сдиралось языком."
            "Пришлось совершить ещё один глоток царской водки цвета марганцовки."
            "На второй раз всё казалось не так плохо, даже начал пробиватсья какой-то вкус."
            iv "Видимо, все рецепторы, отвечающие за кислое, атрофировались."
            "Наконец, с липкими макаронами и резиновыми разобрались, и я, попрощавшись со Славей так же, как поздоровался - кивком, отнёс поднос к мойке, допив по пути компот, и направился в сторону выхода."
        "Сесть с Леной":
            $ pt_un +=1
            "Я решил, что лучше уж поесть в скуке, чем остаться без еды совсем или быть забитым поручениями различного уровня важности для партии."
            ivan "Свободно? Можно присесть?"
            "Поинтересовался я у пустого места - на мой вопрос не было никакой реакции."
            iv "Я даже в наушниках реагирую охотнее!"
            show un shy pioneer close with dissolve
            "Расценив молчание как \"да\", я плюхнулся напротив Лены."
            ivan "Приятного аппетита."
            un "С-спасибо. Тебе тоже."
            "Лена встретила меня своим привычным интровертским дрожанием голоса, потому я решил больше её не доставать."
            "Когда я ем, я глух и нем."
            iv "Что у нас? Макароны, две сосиски с характерным запахом столовой еды и тёплый компот из клюквы и яблок."
            "Моё чувство мне всё же не изменило."
            "Я принялся было сразу глотнуть освежающего напитка, как только начал плеваться."
            "Сахара в этой алой жиже не было совсем, зато кислоты - на тройную дозу!"
            "Зубы и язык резко заныли, а я начал издавать приглушённый гневный стон."
            show un smile pioneer close with dspr
            "Лена обратила своё внимание на меня и заметно улыбнулась."
            iv "То есть как пожелать человеку приятного аппетита, мы депрессивные и невидимые, а как ближнему твоему плохо - сразу в гомерический хохот?"
            show un shy pioneer close with dspr
            "Тем не менее, смех был секундным, и Лена снова обворожительно покрылась румянцем с головы до пят."
            if 'un_p' in list_sch_day2_walk:
                ivan "Лен, ты обдумала моё вчерашнее предложение? Согласна или нет?"
                if pt_un >=6:
                    $ list_sch_day2_walk.remove('un_p')
                    $ list_sch_day2_walk.append('un')
                    "Девочка наконец перестала скрести пустую тарелку вилкой и посмотрела на меня, но, зацепившись за мои глаза, снова стала смотреть на полный гранёный стакан."
                    un "Д-да."
                    "Что \"да?\" Да, обдумала? Или \"да, согласна?\""
                    ivan "Согласна?"
                    "Переспросил я на всякий случай."
                    "С ответом на этот вопрос ей по одному внешнему виду явно потребовалось много усилий, чтобы сказать те же слова."
                    iv "Вот так, дети, убрав несколько слов из предложения, можно усложнить ответ и загнать человека в краску."
                    un "Да…"
                    "Произнесла она с громкостью по идеальной убывающей параболе."
                    ivan "Отлично! Тебе никуда идти не надо?"
                    un "Нет."
                    "В этот раз парабола была возрастающая, такая же, как и на графе моей уверенности в себе ко времени в данный момент."
                    ivan "Жду тебя у входа тогда."
                    un "Х-хорошо."
                    hide un with dspr
                    "Она снова запнулась в начале слова, потому я решил дать девочке допить компот и отнёс поднос, направившись в сторону выхода."
                else:
                    un "Н-нет, извини."
                    "Девочка стала бегать взглядом по сторонам."
                    un "Меня позвали в библиотеку помочь."
                    iv "Ясно, врёт. Сворачиваемся."
                    ivan "Тогда приятного аппетита."
                    "Я взял свой поднос с недоеденной едой и оставил его на мойке, направившись в сторону выхода."
                    hide un with dspr
            else:
                if pt_un >=5 and len(list_sch_day2_walk) == 0:
                    $ list_sch_day2_walk.append('un_p2')
                    ivan "Лена, у меня тут к тебе просьба небольшая появилась."
                    un "К-какая?"
                    ivan "Не бойся, мешки таскать не заставлю."
                    ivan "Как насчёт… Прогулки по лагерю? Вызываю вас, сударыня, на свидание."
                    show un shocked pioneer close with dspr
                    "К такой шутке она была не готова и её лицо изображало крайнюю степень шока."
                    ivan "Извини, не хотел тебя смущать. Меня просто с бегунком послали, а бегать по лагерю в одиночку - скука и нулевая эффективность."
                    "Я пошерстил свёрнутым листком бумаги в моём нагрудном кармане, доказав довольно меркантильное оправдание моей просьбы."
                    ivan "Ну что?"
                    show un serious pioneer close with dspr
                    un "Извини, я занята."
                    "Девочка смотрела мне прямо в глаза"
                    ivan "Может, я могу помочь? Я могу выкрасть часик свободного времени."
                    show un shy pioneer close with dspr
                    ivan "Скажи, что сделать, и я помогу."
                    show un normal pioneer close with dspr
                    un "Мне нужна помощь в библиотеке."
                    "Лена уже не смущалась, но от этого её лицо не стало менее равнодушным."
                    iv "Значит, мне придётся распологать девочку к себе сегодня, раз вчера мне не хватило решимости."
                else:
                    ivan "Извини, что смутил."
                    un "Я н-не сму..."
                    "Она попыталась изобразить в себе равнодушие, но румянец с неё не слетел, а только прибавился."
                    with fade
                    "Наконец, мой поднос был пуст, а компот - последний оплот любого пионера в столовой - был допит, и партия обязывала освободить свободное место для других, кто ещё не успел наесться."
                    "Я отнёс пустую жестяную плиту с тарелками на мойку и двинулся в сторону выхода."
        "Сесть с Ульяной" if pt_dv >=4:
            $ pt_us +=1
            "Я собирался сесть на близкий к Ульяне столик, но чья-то рука с многообщающей... нет, обещающей много неприятностей улыбкой пролезла через пространство между мной и подносом и, ухватив за галстук, потащила на внезано освободившееся место."
            "Очнувшись от секундной потери в пространстве, я осознал, что сижу между рыжим молотом и такой же рыжей наковальней."
            "Ощущения сравнимы с удерживанием в руках два высоковольтных провода с хреновой изоляцией..."
            "Пот течёт по резиновой оплётке и так и новорит стечь к чуть ли не искрящемуся алюминию, готовому отдать несколько электронов отличному проводнику."
            "Мне дали опустить поднос на стол, но галстук не отпустили, а наоборот, потянули и я был прижат к чему-то объёмному, упругому и мягкому."
            show us grin sport close pioneer at cleft
            show dv laugh pioneer2 veryclose at cright
            with dissolve
            "Лицо заразы с карамельными волосами было на опасно близком расстоянии, а когда я опустил взгляд на локоть..."
            "Да, он упирается именно туда, куда я подумал."
            dv "Ловко ты обошёл меня на раздаче. Как сделал, расскажешь?"
            "Свою тайну я выдавать решительно не хотел, потому решил сменить тему."
            ivan "Алиса, ты бы отпустила меня, а то дети смотрят."
            "Намёк на Ульяну был воспринят с насмешкой - и меня ещё сильнее придавили к себе, в итоге моя рука оказалась чуть ли не в положении медианы по отношению к туловищу Двачевской."
            show dv grin sport veryclose at cright with dspr
            dv "С этим есть какие-то проблемы?"
            ivan "Да нет, но... Не слишком рано?"
            dv "Да не беспокойся, никто не заметит."
            ivan "Да? Ну тогда разрешите расстегнуть мешающий мне узел на вашей рубашке."
            "В ответ последовало красноречивое впивание изящных девичьих ногтей прямо в моё лёгкое через рёбра."
            ivan "Намёк понят. Что хотела?"
            dv "Хотела? Ой, сущие мелочи."
            "Она снова улыбнулась своей многообещающей улыбкой."
            dv "Пару вопросиков."
            ivan "А если не отвечу?"
            "Пальцевая дробь по моим многострадальным рёбрам снова явилась ответом в стиле \"вместо тысячи слов\"."
            ivan "Давай. Спрашивай."
            dv "Начнём с простого. Откуда ты?"
            "Действительно простой вопрос."
            menu:
                "Из Москвы":
                    $ sch_day2_dv_rightanswers +=1
                    dv "Богатый, видать, пионер. Из Москвы. Ну, что, Иван. Что в Москве делаешь? А в штанах \"Столичную\" хранишь?"
                    ivan "Студент. Спрашиваешь, почему в Москве живу? Потому что родился. Потому что столица. В штанах кое-что другое держу."
                    iv "А вот если бы я сейчас трубил, что данная шутка была придумана под моей редакцией, то меня бы назвали каким-нибудь \"амбалом\"."
                "Из Санкт-Петербурга":
                    $ pt_ka +=10
                    "Алиса на пол секунды призадумалась."
                    ivan "Из батюшка Ленин-града."
                    with flash_red
                    "Мне снова впились в кожу ногтями."
                    dv "И без тебя знаю."
                    us "Ха-ха, Алиска необразованная! Про Ленинград не знает!"
                    "От моих рёбёр отстали и я краем заметил кулак, направленный в сторону мелкой."
                    "Ответа не последовало."
                "Из будущего":
                    $ pt_ka -=10
                    dv "Шутка засчитана."
                    ivan "Да я не..."
                    "Возражение умерло у меня в голосовых связках."
                    iv "А что я ей скажу? Потом от меня все будут шарахаться, а заберёт меня к себе само КГБ, как шпиёна."
            dv "Итак, следующий вопрос."
            ivan "Мне начать бояться?"
            dv "О да. Вряд ли ты на него ответишь. Сколько тебе лет?"
            menu:
                "Восемнадцать":
                    $ sch_day2_dv_rightanswers +=1
                    dv "Отлично! Значит, тебе могут продавать алкоголь!"
                    ivan "С чего такая радость?"
                    dv "А что, не могут?"
                    ivan "А что, я соглашусь?"
                "Семнадцать":
                    $ pt_dv +=1
                    $ pt_ka +=10
                    dv "Вот блин."
                    "Выглядела она обиженной настолько, будто упустила какую-то возможность."
                    ivan "А что не так?"
                    dv "Да молодой ты ещё?"
                    ivan "А ты предпочитаешь старых?"
                    "Повторное простукивание моих ребёр было ответом."
                    dv "Да вот бесполезный ты какой-то."
                    ivan "Да я? Бесполезный? Да я много чего могу!"
                    dv "Пока, кроме движения в шортах, я никаких действий не увидела."
                    ivan "А вдруг у меня там \"Столичная?\""
                    show dv shocked pioneer2 veryclose at cright with dspr
                    "Я увидел в Алисе смущения? Ударьте, если это правда."
                    dv "Что, серьёзно?"
                    ivan "А ты проверь."
                    show dv laugh pioneer2 veryclose at cright with dspr
                    dv "Ладно, 1:1."
                    ivan "Откуда один у тебя?"
                    dv "Так за прошлый вопрос!"
                    ivan "В смысле?"
                    dv "Здесь я вопросы задаю."
                "Двадцать семь":
                    $ pt_ka -=1
                    $ pt_dv -=1
                    show dv sad pioneer2 veryclose at cright with dspr
                    dv "Не смешные у тебя шутки."
                    ivan "Да что не так-то? В душе точно двадцать семь."
                    dv "Проехали."
            if sch_day2_dv_rightanswers == 2:
                $ pt_dv +=2
            show dv grin pioneer2 veryclose at cright with dspr
            dv "Ну и последний, третий вопрос. Очень важный, отвечай правильно!"
            ivan "Валяй."
            dv "Впервые к груди притронулся?"
            ivan "Да ну тебя!"
            show dv laugh pioneer2 veryclose at cright with dspr
            "Алиса рассмеялась и наконец ослабила хватку, но не выпустила меня."
            iv "А еда-то стынет!"
            "Да и насущные вопросы у рыжей кончились, и она начала выискивать, что интересного есть в моей форме."
            "Побила свободной рукой по карманам, пролапала всю мою рубашку, пока не зацепилась за нагрудный карман."
            dv "А что это тут у нас? Какая интересная бумажка!"
            show dv normal pioneer2 veryclose at cright with dspr
            "Она одной рукой её развернула, порассматривала и, сложив обратно, небрежно запихнула ко мне в карман обратно с угасшим интересом."
            iv "Интересно, что она там ожидала найти?"
            dv "С бегунком послали? Какая жалость."
            ivan "А у тебя ко мне дело?"
            if pt_dv >=6:
                dv "Вообще, есть. Но ты же бегунок, да и вылитый маменькин сынок. Наверняка ещё за Славей гонишься."
                "Логика у неё, конечно, гениальная. Ношу форму по-обычному или не одет в спортивную - ты не с нами."
                ivan "Интересные догадки, рыжая. Говори, какое дело, может у меня тоже к тебе дело есть."
                dv "Музыку любишь?"
                iv "Оп-па. Заявки на гитариста в этой? Хотя да, гитара да барабаны - орудие борца с системой, не иначе."
                ivan "Ну допустим. А ты?"
                dv "Конечно."
                ivan "Двачевская."
                dv "Что?"
                ivan "Руку отпустишь?"
                show dv laugh pioneer2 veryclose at cright with dspr
                dv "Не-а! У тебя остался один вопрос."
                ivan "Бегать любишь?"
                dv "Люблю."
                "Ответила рыжая без вопросов."
                ivan "А квесты любишь?"
                show dv grin pioneer2 veryclose at cright with dspr
                dv "Это намёк на ролевые игры?"
                ivan "Почти. Ну так что хотела?"
                dv "Поможешь с аппаратурой?"
                ivan "А что-то нужно починить?"
                dv "А ты умеешь? Тогда ты обязательно потребуешься."
                ivan "А кибернетики уже не устраивают?"
                dv "Да заняты они! Со своим роботям две недели возятся, переспать хотят. Ещё корпус с главными женскими атрибутами."
                ivan "Кхм. Чем смогу, если инструмент дадите."
                dv "Ворованый подойдёт?"
                ivan "Если я об этом не узнаю, то да."
                dv "Отлично. Твоё встречное предложение?"
                ivan "Ты и лично ты проведёшь меня по лагерю!"
                dv "Я? С какой стати я? У тебя карта есть, вот и бегай."
                ivan "Да ладно тебе, Двач. Ты же всё знаешь, наверняка скажешь, где подпись получить быстрее."
                dv "Ладно. Договорились. Жду тебя у клуба кбирнетиков после завтрака."
            us "Ти-ли ти-ли!"
            "Подала голос Ульянка, убежав с подносом."
            hide us with dissolve
            show dv normal pioneer2 close at center with dspr
            "Наконец, меня опустили и дали поесть."
            "И как только я отвлёкся на еду, то заметил..."
            "Две сосиски из двух исчезли."
            ivan "Ульяна, мать твою..."
            show dv laugh pioneer2 close at center with dspr
            dv "Не будь жесток с ребёнком, она ни в чём не виновата."
            ivan "Да будь она трижды младенцем, я бы ей по мягким местам гибким прутом..."
        "Сесть одному":
            if pt_mi >= x:
                # Мику подсаживается
                pass
            else:
                # нуар
                pass # временно
