label sch_day0_prehistory:
    $ prolog_time()
    $ persisent.sprite_time = 'Night'

    play music distant fadein 2
    $ renpy.pause(1, hard = True)

    show unblink

    # Предыстория

    scene bg ext_winterpark with fade

    voice "Ты же не забыл, что я скоро уеду?"
    ivan "Ты тоже меня бросишь, как и все."
    voice "Не обижайся, но я правда ничего не могу обещать. Прости. Я уезжаю туда, где ты меня вряд ли найдешь."
    ivan "Куда-нибудь в Америку, в свободные штаты?"
    voice "В точку. И, скорее всего, я не вернусь назад."
    "Молчание становилось невыносимым."
    "В холодном воздухе мерцали снежинки, смешиваясь с мерным человеческим дыханием."
    "Вдох. Выдох. Холодно."
    ivan "Но мы же сможем поддерживать связь? На что нам даны соц. сети?"
    voice "Ничего не могу обещать. Если не потеряемся."

    if not sch_true:

        scene bg semen_room with fade

        "Как обычно. Ничего нового. Случайное сообщение в ВК после 10 лет разлуки. Встреча. Разговор. Все, как прежде. И ради чего? Чтобы все закончилось вот так?"
        "Ненавижу."

        scene bg ext_winterpark with fade

        voice "Знаешь, мне иногда кажется, что в один прекрасный день кто-то из нас не проснется."
        ivan "Это ты к чему?"
        voice "Да так, вспомнилось. Неохота идти всю дорогу молча. Ты прости уж."

        scene anim prolog_2 with fade


        "Длинный гудок. Удалённый аккаунт с изменённой фамилией."
        "Прошёл год."
        "Три. Шесть. Пять."

        with fade

        "А я никогда не забуду."
        "Все, что у меня осталось, так это воспоминания. Я ловлю, стараюсь ухватиться за прошлое…"
        "Безрезультатно. Образ навсегда растворился во мне, в моей памяти. Прости, прощай. Я тебя никогда не забуду."
        "Воспоминания сменяются другими. Кадр. Пленка. Момент. Мелькнувшая рука, словно вспышка света, оставляющая резкий след на плече, который греет и жжет, как майское солнце. След."
        "Растворился. Навсегда. Ты умер. По слогам произношу имя, и все, что вижу, так это твою расплывчатую улыбку, котрая шепчет мне: \"Мы не встретимся зимой. Не жди меня. Я не вернусь.\""

    else:

        "Холодно. Колко. Неуютно."


        ivan "Знаешь, мне иногда кажется, что в один прекрасный день кто-то из нас не проснется утром. Один раз мы задохнемся во сне. Хватай кислород, пока живешь. Смерть не любит свежий воздух."
        voice "Да что ты такое говоришь?!"
        ivan "Да так, просто к слову. Хотел разбавить наше неловкое молчание."

        pause(1)

        voice "Ну что? Пришла пора расстаться?"
        ivan "Да, пора. Прости. Прощай. Лучше сейчас, чем никогда."

        "Мы по-дружески обнялись и пожали друг другу руки."

        "Развернулись и..."

        scene bg semen_room_day with fade

        pause(1)

        "Каждый получает то, к чему стремится."
        "По заслугам."
        "Я заслужил пророчество."
        "Оно было чужим, не моим."
        "Оно произносилось не моим голосом, исходило не из моего разума."

        ivan "Ты проиграешь все деньги."

        "Говорю я одному из своих знакомых."
        "Он смеется, сверкая глазами, в которых я вижу азарт, а на следующий день блеск гаснет, он застывает на месте с раскрытым кошельком, а там…  пусто. Он промотался до нитки. В Вегасе или в Лос-анджелесе? Да неважно. Казино тебя убило."

        ivan "Тебя кредит погубит, ты понимаешь?."

        "В ответ уверенный смешок. На следующий день - новая Ламборджини жёлтого цвета. Через неделю лишь груда металла. А через год - ни автомобиля, ни дома."

        "Этот голос… он не принадлежит мне. Он исходит откуда-то изнутри."
        "Он знает всё. Знает то, чего не знаю я. С древнейших времен. Он видит меня. Каждый мой шаг."

        ivan "Берегись автомобиля! Хрустальная ты моя."

        "Шучу я со своей знакомой, провожая ее до пешеходного перехода..."
        "Как потом я узнал, это была наша последняя встреча."

        scene bg semen_room_night with dissolve

        iv "Ты... {w=0.5} Тебя... {w=1} Ты."

        "Они, но не я."
        "Им, но не мне."

        ivan "Да что со мной не так? Где я ошибся?"
        ivan "Что я сделал?!"

        "Каждый мой шаг. Меняет время, пространство, судьбы людей. Эхом воспоминаний, густой тенью прошлого."
        "Я - Пророк."
        "Я прошел в финал ток-шоу с главным призом - истинной жизнью. Я вижу вас насквозь. Вы такие забавные, независимые. А потом несчастливая карта, скользкая дорога - и все. Вас нет. А я есть."
        "Один : Один. Ничья. Я прошел в финал."
        "Но только в финал чего?"
        "Никто тут из нас не выиграл."
        "Но каждый из нас проиграл."


    show blink

    show unblink

    if not cycled:
        show bg semen_room behind blink
        hide blink
        show unblink

    "Привет, Реальность."
    "Какие события разбавят мой сегодняшний день?"
    return


label sch_day0_keys:
    $ prolog_time()
    $ persisent.sprite_time = 'Night'

    #TODO

    # Мику - браслет (привязанность)
    # Славя - ?
    # Нуар - монетка (аверс-реверс, удача, Рандом)
    # Одиночка - перо (писательство, память)
    # Алиса - зажигалка (огонь, творчество)
    # Ульяна - карта (поиск)
    # Лена - Книга Селинджера "Над пропастью во ржи"

    return

label sch_prologue_chose:
    $ prolog_time()
    $ persisent.sprite_time = 'Night'

    play music cassiopeia fadein 1

    scene anim prolog_2

    with flash

    if sch_true:

        "Яркий свет, не покидай меня."
        "Я хочу в тебе раствориться."
        "Но он глух. И слеп. Настолько же, насколько и ярок."

        dreamgirl "Ты пойдёшь со мной?"
        ivan "В который раз ты уже спрашиваешь..."
        "И в который раз я отвечаю..."

        menu:
            "Я ничего не обещаю.":

                $ limb = True

                dreamgirl "Я понимаю."

                "Я начинаю забывать, кто я. И просыпаюсь. Впадаю в Реальность. Не буди меня. Я устал воскрешать себя утром."

                with flash

            "Я приду." if sch_true:

                $ prophet = True

                dreamgirl "Я знаю."



    elif cycled:


        "Яркий свет рассеялся. Вот оно. Откровение. Я все вспомнил."
        "Мне снова снится тот же сон, тянущийся из глубокого детства. Он повторяет мне одно и то же. Напоминает о моих ошибках, говорит о моих проколах и падениях. Зачем ты мне снишься? Уйди, прошу тебя."
        "{b}Она{/b} говорила отрывисто, уверенно, но я её не слушал."
        "{b}Она{/b} знала что-то важное, чего не знаю я. Может, это и есть моя судьба?"
        dreamgirl "Ты пойдёшь со мной?"
        ivan "От меня ничего не зависит. Сама знаешь."

        "И тот ли это Иван стоит сейчас передо мной, во сне. Тот ли настоящий я, которого я знаю всю свою жизнь?"
        "И мой ли это сон?"

        ivan "Я ничего не обещаю. Сама знаешь. Осторожность."

        $ limb = True

        dreamgirl "Я понимаю."

    else:
        $ name_sch('Я')

        show blink

        show uvao_d0 at cleft behind blink
        show anim prolog_2 behind uvao_d0

        pause(1.5)

        hide blink with dspr

        stop music fadeout 1
        play music cassiopeia fadein 1

        "Мне снова снится тот же сон, тянущийся из глубокого детства."
        "И снова я видел ту девочку в коротком, оборванном платьице, которое шуршит на ветру, когда так ловко поднимаются потрёпанные складки, что кажется, будто море, огромное лазурное море разливается по свежему лугу, оставляя после себя сладкий запах соленой волны." # Контекст, сцуко!

        dreamgirl "Ты пойдёшь со мной?"

        "Куда пойду? О чём она?"

        dreamgirl "Всё или ничего."

        ivan "От меня ничего не зависит. Сама знаешь."

        "Разве это я? Тот самый?"

        dreamgirl "Так попытайся. Просто протяни руку."

        "И я ли тут нахожусь?"
        "И мой ли это сон?"
        "И сон ли это вообще?"

        $ limb = True

    dreamgirl "Прощай."

    stop music fadeout 1

    return


label sch_prologue_normal:
    $ prolog_time()
    $ persisent.sprite_time = 'Night'

    $ set_mode_nvl()

    scene anim prolog_2 with fade

    play music nullspace fadein 1

    $ nvl show dissolve

    "Мне 7 лет. Я впервые иду в школу."
    "Первое сентября, свежий букет с золотыми розами, томительное ожидание чего-то великого и важного. Надежда на счастье и беззаботную жизнь. Я стою в толпе наряженных первоклассников. Букет закрывает мое заплаканное лицо. Я повторяю себе: \"Сегодня важный день.\""
    "Но не для меня."

    $ name_sch('Ваня')

    ivan "Мама! Мамочка, ты где?"
    iv "Мне очень страшно."
    voice "Эй, малыш, не плачь! Ты в порядке? Спокойно, спокойно. Ты ищешь маму?"
    ivan "Да... {w=0.7} Ты знаешь, где она?"

    nvl clear

    with dissolve

    "Большой праздник. Свечи, торт, серпантин, яркие гирлянды, хлопушки и пряный запах свежих мандаринов. И тут кто-то всхлипнул в углу. Так неловко и осторожно, будто боялся разрушить хрустальную атмосферу волшебства. Ребенок плачет. А где-то над потолком блестит яркий, красный шарик."

    iv "Эта девочка мне помогает. Какая она хорошая!"
    iv "Я бы точно-точно на ней женился!"

    voice "Давай знакомиться. Как тебя зовут? Мама точно гордится твоим именем!"
    ivan "Меня зовут Иван!"

    iv "Говорю как самый настоящий взрослый, где формальность - признак важности. Говорю так важно и пафосно, словно я - не просто потерявшийся мальчик, а самый настоящий взрослый Иван, такой серьезный и сильный, прячущий в своих маленьких, сжатых кулачках весь груз взрослой жизни. Я - Иван. И это вам не шутки, говорю. Я сильный. Поэтому мама меня ищет. А я ищу ее."

    voice "А меня - Юля. Пойдём искать твою маму?"

    nvl clear

    with dissolve

    iv "И мы растворяемся в потоке человеческих тел, ног, рук, глаз, мелькавших брюк и платьев. Я шагаю по бульвару, руку незнакомого, но доброго человека, гордо шлепая по лужам и оглядывая случайных прохожих тем любопытным взглядом, который всегда означает юность и детскую неопытность, любопытство свежего человеческого ума. Я - Иван. Я взрослый. Мама меня ждет."

    mother "Ваня! Ванечка… как ты меня напугал. Спасибо большое, что нашли моего сына. Больше не убегай от меня, слышишь? Ни на шаг!"

    iv "Но я стою и не отпускаю руку этой девочки. Она такая теплая и мягкая. Как у мамы…"

    mother "Что с тобой?"

    iv "А теперь она смеётся. Но не надо мной же? Она не может надо мной смеяться. Она слишком добрая."
    iv "И она наклоняется ко мне."
    iv "Я вижу её лицо."
    iv "Идеальные черты лица, жёлтые сверкающие глаза, складки розовых губ… Она плачет? Я что-то сделал не так?"

    ivan "А мы с тобой ещё увидимся?"
    voice "Обязательно, дорогой. А теперь иди к маме, тёте пора. И не теряйся больше!"

    iv "Я с неохотой отпускаю добрую руку и бросаюсь в объятия мамы."

    nvl clear

    with Fade(3)

    $ renpy.pause(3, hard = True)

    iv "7.20. Будильник. Такой звонкий, хоть уши затыкай. Снова в школу, а зачем, спрашивается?"
    iv "Опять сидеть за партой и слушать, слушать, слушать."
    iv "Слушать, как складываются, делятся, вычитаются и умножаются цифры? Как слова складываются предложения, как Колумб открыл Америку? А для чего? Я и без цифр хорошо жил, а Америку вижу только на картинках в учебнике."

    mother "Пойдём, а то опоздаем."
    ivan "Но я не хочу!"
    mother "Сынок, я все понимаю. Но есть такое слово - надо. Ты же настоящий мужчина! Сильный и храбрый. А разве такие говорят: \"Не хочу?\""

    iv "\"Надо\" - слово как слово. И от чего оно считается таким особенным? Ведь другие слова тоже важны. Без слова \"Мама\" ты не позовёшь маму, без слова \"Привет\" ты ни с кем не поздороваешься."
    iv "Но иногда слова такие глупые!"

    nvl clear

    with dissolve

    pause(0.5)

    mother "Мне учитель сказал, что у тебя нет друзей. Совсем-совсем нет? Почему ты ни с кем не общаешься? И почему не говоришь об этом мне?"
    ivan "А мне никто и не нужен. Кроме мамы."

    iv "И той девочки."

    mother "Если не найдёшь себе друзей, то как ты будешь жить дальше? Всю жизнь один? Неужели ты не понимаешь, что без друзей никуда?"

    iv "И кто это сказал? Мне и без друзей хорошо живётся."

    nvl clear

    with dissolve

    pause(2)

    chat "Привет! Давай дружить!"

    ivan "Меня зовут Ваня! Давай! Только ты не покинешь меня?"

    nvl clear

    with dissolve

    iv "Мне 12. Шестой класс."
    iv "Я снова один."
    iv "Потому что слишком медленный. Может, я чего-то не понимаю?"
    iv "А потом переезд. Коробки, пыль, вечная возня, мебель, закрытая чехлами, мой рюкзачок с солдатиками и любимой раскраской. У меня нет времени, чтобы попрощаться со знакомыми. Машина. Сидение. Я сижу с мамой, оглядывая испуганными глазами серый панельный фасад новенького здания, которое больше никогда не будет называться моим домом."

    nvl clear

    with dissolve

    iv "Трудности переезда. Трудности отношений в новой школе. Страх, отчаяние, слезы. И нескончаемая тоска. По дому."
    iv "Я все ещё переписываюсь со старыми знакомыми, начинаю пробовать себя в искусстве."

    nvl clear

    with dissolve

    iv "Мне 15 лет. Я чувствую себя взрослым и умным, но розовые очки той реальности, что создали мне родители и учителя, начали трескаться."
    iv "Рисование мне надоело почти сразу же, как и фотография. И не потому, что я был ленив или меня трудно в чем-то заинтересовать. Просто я не видел в этом истинного смысла. Потом я пробовал писать музыку. Обучался этому около года, и тоже забросил. Попробовал себя в писательстве. Кажется, что нашёл себя, но снова провал. Я опять себя обманул. Как обычно."

    nvl clear

    with dissolve

    stop music fadeout 1
    play music cassiopeia fadein 1

    iv "А по ночам мне снится та девушка с янтарными глазами."

    nvl clear

    $ set_mode_adv()

    show uv normal

    with dissolve

    $ sch_name('Я')

    ivan "Ты обещала, что придешь ко мне."
    ivan "Слышишь? Ты обещала."
    ivan "И что теперь? И зачем?"
         "Но я все еще жду…"

    scene semen_room_night with dissolve
    $ renpy.pause(2, hard=True)
    scene semen_room_day with dissolve

    "Мне 27 лет. Снаружи тяжёлые латы - зимнее чёрное пальто, полное пустых карманов, а внутри хрупкий моторчик качает пару литров красной краски."
    "Это я. И вся моя жизнь."

    scene semen_room_sunset
    $ renpy.pause(1, hard=True)

    "И снова творчество. Правда, времени нет почти, всё в делах, всё в работе."
    "Да и нечем восполнить обескровленное сердце, которое толкает в организм даже не краску - а лишь пустые, полупрозрачные обещания и слова с серым оттенком тоски."
    "Потом эта неприятная ситуация с последним человеком, который был мне дорог и покинул меня."
    "А что сейчас? Хочется сбежать от Реальности в свой выдуманный мир с перегруженной басом музыкой, где есть свет, где есть счастье и любовь. Ну а пока что моим персональным раем является синий экран заблокированного мессенджера, в котором я общаюсь, чтобы не потерять рассудок."

    scene bg int_home_lift_sch with fade

    "Засыпает город, \nА тебе не спится."

    window hide

    $ renpy.pause(2)

    stop sound_loop fadeout 1
    pause(1)
    play ambience ambience_cold_wind_loop fadein 3
    play sound sfx_intro_bus_stop_steps

    scene anim intro_2

    with fade

    $ renpy.pause(3, hard=True)

    scene anim intro_3

    with fade

    $ renpy.pause(3, hard=True)

    scene anim intro_4

    with fade

    "Каждый получает \nТо, к чему стремится."
    if sch_true == 1:
        "19/1987/last..."
        "С этого всё началось, этим всё и закончится."

    else:
        pass

    $ renpy.pause(1, hard = True)

    window hide

    scene bg bus_stop with dissolve

    "Я снова стою на своей остановке, самой далёкой для маршрута."
    "Куда я еду? Не знаю. Может, туда, где лучше?"

    stop ambience fadeout 1
    stop music fadeout 1
    play sound sfx_intro_bus_engine_start

    $ renpy.pause(3)

    play sound_loop sfx_intro_bus_engine_loop fadein 3

    if (sch_true) or (sch_bound):
        "410. Наш автобус отправляется в ад. Три. Шесть. Пять."

    else:
        pass

    play sound_loop sfx_intro_bus_engine_loop fadein 3

    window hide

    $ renpy.pause(2)

    scene anim intro_10
    with fade

    play sound sfx_intro_bus_door_open

    $ renpy.pause(3, hard=True)
    scene anim intro_11

    with fade

    $ renpy.pause(1, hard=True)

    stop sound_loop fadeout 4

    scene bg intro_xx

    with fade

    stop ambience fadeout 2
    play sound_loop sfx_bus_interior_moving fadein 4

    window show

    play music distant fadein 1

    $ set_mode_nvl()

    nvl show dissolve

    "Наша жизнь - всего лишь бесконечная череда бесконечных событий, происходящих в один момент."
    "Кто-то умирает, кто-то рождается, а кто-то стоит посреди города с растеряным лицом и не знает, что делать дальше."
    "Наверное, чтобы окончательно не потеряться в бесконечном течении временных параллелей, люди придумали Время."
    "Оно все упрощает. Оно беспощадно."
    "И каждый из нас становится медленным самоубийцей, бесцельно растрачивающим свои драгоценные минуты жизни."
    "Момент сменяется моментом."
    "Слова заменяются эмоциями."

    $ renpy.pause(1, hard=True)

    "Мир не стоит на месте."

    pause(0.5)

    "И никто не имеет права этого изменить."

    nvl hide dissolve

    nvl clear

    $ set_mode_adv()

    window hide

    show blink

    show bg intro_xx behind blink

    show unblink
    hide blink

    $ renpy.pause(2, hard=True)

    stop sound_loop fadeout 1
    play sound sfx_intro_bus_engine_start

    "Пустая остановка и запах свинцового дыма из выхлопной трубы уехавшего автобуса."
    "Придётся идти домой пешком."

    show bg bus_stop with fade

    $ renpy.pause(3)

    play sound wind
    play music connor

    show bg bus_stop_summer with dissolve(.7)

    pause(0.7)

    play sound wind

    show bg bus_stop with dissolve(.7)

    ivan "Что?!"

    show blink

    pause(1)

    show bg bus_stop_summer with dissolve

    show unblink
    hide blink

    play sound wind

    pause(1)

    show blink

    $ name_sch("Иван")

    ivan "А-а-а-а!"

    pause(0.7)

    play sound wind

    scene bg black with fade

    play sound sfx_bodyfall_1

    $ renpy.pause(2)

    return


label sch_prologue_true:
    $ prolog_time()
    $ persisent.sprite_time = 'Night'

    "Я не протагонист, и не враг мира. Я враг самого себя."
    "Персонаж второго плана."
    "Но ведь есть всегда тот, кому хуже."
    "Или чья жизнь будет достаточно интересна для экранизации."
    "Только если со мной не случится чуда, моя жизнь так и останется за ярким билбордом со всемирно известной надписью \"Голливуд\""

    $ renpy.pause(1, hard=True)

    "Я каждый день прокручиваю события того дня. И каждый раз я узнаю что-то новое."
    "Как будто кто-то хотел его смерти."

    $ renpy.pause(1, hard=True)

    "Каждую ночь я вижу чьи-то глаза, метающие свои жёлтые искры прямо в мою душу."
    "Они смотрят на меня с той непреодолимой тоской, с которой смотрят глаза матери, отправляющей своего сына на войну..."
    "Они что-то от меня хотят."
    "Каждый день они передо мной. Говорят, что взгляд можно ощутить своей кожей. Тактильное прикосновение разума."
    "На утро я просыпаюсь в холодном поту."

    scene bg semen_room_night with fade

    ivan "Она что-то хочет сказать мне."

    "Говорю я в роковую пустоту."

    #TODO

    pause(3)

    scene anim prolog_2 with fade

    return


label sch_prologue_normal_end:
    $ prolog_time()
    $ persisent.sprite_time = 'Night'

    play ambience ambience_camp_center_night fadein 2

    $ renpy.pause(2)

    play music aire fadein 1

    scene cg uvao_d0 with dspr

    dreamgirl "Ты как?"

    "Меня встретила даже не девушка - девочка - в коротком порванном мини-платье и бинтом на ноге."
    "Её жёлтые глаза кого-то мне напоминали, но кого?"

    show anim prolog_2 behind uvao_d0
    show uv normal at left

    hide cg uvao_d0

    with dissolve

    $ name_sch("Я")

    ivan "Где я?"
    dreamgirl "Там, где тебе нужно оказаться."
    ivan "Дай угадаю - пока я сейчас мило беседую с тобой, где то-там, за пределами моего сна, добрые санитары утирают пену с моего рта и застегивают за спиной рубашку смиряющего действия?"
    dreamgirl "Понятия не имею, о чём ты, но все это не сон."

    with flash_red

    show uv normal at left

    "Ай!"

    ivan "Ты чего? Могла просто ущипнуть!"
    dreamgirl "Зато ты убедился, что я существую."
    dreamgirl "Может быть, мы продолжим наш разговор?"
    ivan "Кстати о разговоре - с кем имею честь?"

    show uv upset at left with dspr

    dreamgirl "Перестань притворяться - обратно я тебя всё равно не верну."

    pause(1)

    show uv normal at left with dspr

    dreamgirl "Не узнаёшь свой сон? Мне пора обижаться?"

    show bg ext_entrance_night_water_sch behind uv with dspr

    "Передо мной показась дверь с яркой неоновой вывеской. Напрягая свой взгляд, я смог прочитать знакомое слово \"Вход\"."
    "Но куда он ведёт? Буквы казались размытыми и я с трудом мог разобрать написанное слово."

    ivan "Ты можешь не играть в угадайку?"

    "Меня начала раздражать эта нелепая таинтсвенность."

    iv "Сколько я тут вообще нахожусь?"

    "Спрашивать о чем либо эту странную девочку также бессмысленно, как слушать новости по первому федеральному или обновлять ленту соц. сети каждые пол минуты."

    menu:
        "Отпусти меня.":
            ivan "Я не знаю, где я, но я просто хочу вернуться назад."
            dreamgirl "Дело твоё. Если твои атомы не разобросает по всей вселенной, то ты сможешь обдумать все, чтобы разобраться в себе, и, возможно, даже вернуться."

            with fade

            $ deathflag = True

        "Что тебе нужно?":
            dreamgirl "От тебя? Чтобы ты поверил мне."
            ivan "Допустим, я верю."
            dreamgirl "Тогда пойдём."

            scene cg uvao_d0 with dissolve

            "Она подошла ко мне настолько близко, что я мог чувствовать её дыхание. Мята. Холодно."

            scene cg uvao_d0_2 with dissolve

            dreamgirl "А теперь..."
            dreamgirl "Спи..."

            stop music fadeout 1


    window hide

    scene bg black with fade

    if not deathflag:
        play sound wind

    pause (0.5)

    play sound sfx_bodyfall_1

    return

label sch_prologue_true_end:
    $ prolog_time()
    $ persisent.sprite_time = 'Night'

    play music finale fadein 1

    scene bg ext_entrance_night_clear_sch
    show uvao_d0

    with dspr

    "И просыпаюсь."
    "Тот же, там же."

    dreamgirl "Снова встретились."
    ivan "Как по таймеру."
    dreamgirl "Ты готов?"

    "Она снова мне протянула свою холодную руку."
    "Я снова взял её и снова не мог её расцепить."
    "Да и не особо хотел - теперь я знал наверняка…"
    "И получил возможность..."

    pause (0.3)

    "Исправить свою жизнь..."
    "И сделать всё правильно. С самого начала. С чистого листа."

    dreamgirl "Brace yourself, your last chance..."

    "Напевала мне моя подруга."

    ivan "Last summer..."

    $ renpy.pause(3, hard=True)

    play sound wind

    stop ambience fadeout 1
    stop music fadeout 1

    $ volume(1.0, 'music')

    return


label sch_prologue_death:
    $ prolog_time()
    $ persisent.sprite_time = 'Night'

    $ sch_chapter(0, u"Game over.")

    scene bg ext_liaz with dspr

    play music markus fadein 1

    "Меня встретил мир. Такой холодный, манящий, такой чужой и далекий, что я в нем - как иголка в стоге сена. Весь в слезах, потный, рваный, потерянный, я лежал под свинцовым небом, разрезая взглядом проплываюшие облака. Если я закрою глаза. То этого больше не будет. В памяти звучит только одна фраза. Я пойду с тобой. Чего бы этого не стоило. Я готов."
    "Я вздрогнул и погрузился во тьму."

    ivan "Прощай, Реальность."

    show bg black with fade

    $ renpy.pause(5, hard=True)

    stop music fadeout 5
    stop ambience fadeout 1

    $ renpy.pause(3)

    $ volume(1.0, 'music')

    return
